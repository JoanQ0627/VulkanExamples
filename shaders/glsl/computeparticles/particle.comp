/* Copyright (c) 2016-2025, Sascha Willems
 *
 * SPDX-License-Identifier: MIT
 *
 */

#version 450

struct Particle
{
	vec2 pos;
	vec2 vel;
	vec4 gradientPos;
};

layout(std140, binding = 0) readonly buffer ParticleSSBOIn {
   Particle particlesIn[ ];
};

layout(std140, binding = 1) buffer ParticleSSBOOut {
   Particle particlesOut[ ];
};

layout (local_size_x = 256) in;

layout (binding = 2) uniform UBO 
{
	float deltaT;
	float destX;
	float destY;
	int particleCount;
} ubo;

vec2 attraction(vec2 pos, vec2 attractPos) 
{
    vec2 delta = attractPos - pos;
	const float damp = 0.5;
    float dDampedDot = dot(delta, delta) + damp;
    float invDist = 1.0f / sqrt(dDampedDot);
    float invDistCubed = invDist*invDist*invDist;
    return delta * invDistCubed * 0.0035;
}

vec2 repulsion(vec2 pos, vec2 attractPos)
{
	vec2 delta = attractPos - pos;
	float targetDistance = sqrt(dot(delta, delta));
	return delta * (1.0 / (targetDistance * targetDistance * targetDistance)) * -0.000035;
}

void main() 
{
    // Current SSBO index
    uint index = gl_GlobalInvocationID.x;
	// Don't try to write beyond particle count
    if (index >= ubo.particleCount) 
		return;	

    // Read position and velocity from previous frame
    vec2 vVel = particlesIn[index].vel.xy;
    vec2 vPos = particlesIn[index].pos.xy;
    vec4 gPos = particlesIn[index].gradientPos;

    vec2 destPos = vec2(ubo.destX, ubo.destY);

    vec2 delta = destPos - vPos;
    float targetDistance = sqrt(dot(delta, delta));
    vVel += repulsion(vPos, destPos.xy) * 0.05;

    // Move by velocity
    vPos += vVel * ubo.deltaT;

    // collide with boundary
    if ((vPos.x < -1.0) || (vPos.x > 1.0) || (vPos.y < -1.0) || (vPos.y > 1.0))
    	vVel = (-vVel * 0.1) + attraction(vPos, destPos) * 12;
    else
    	particlesOut[index].pos.xy = vPos;

    // Write back
    particlesOut[index].vel.xy = vVel;
	particlesOut[index].gradientPos.x = gPos.x + 0.02 * ubo.deltaT;
	if (particlesOut[index].gradientPos.x > 1.0)
		particlesOut[index].gradientPos.x -= 1.0;
}

